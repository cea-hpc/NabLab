/*******************************************************************************
 * Copyright (c) 2022 CEA
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 * Contributors: see AUTHORS file
 *******************************************************************************/
module ExplicitHeatEquation;

with Math.*;
with CartesianMesh2D.*;

// Simulation options
ℝ stopTime;
ℕ maxIterations;

let ℝ u0 = 1.0;
let ℝ[2] vectOne = ℝ[2](1.0);
let ℝ δt = 0.001;
ℝ t;
ℝ[2] X{nodes}, Xc{cells}; // Position of nodes and cells center of gravity 
ℝ u{cells}; // Temperature
ℝ V{cells}; // Volume of cells
ℝ D{cells}; // Cell centered conductivity
ℝ faceLength{faces}, faceConductivity{faces};
ℝ αExtraDiag{faces};
ℝ α{cells, cells};

iterate n while (t^{n+1} < stopTime && n+1 < maxIterations);

InitTime: t^{n=0} = 0.0;

InitXc: ∀c∈cells(), Xc{c} = 0.25 * ∑{p∈nodesOfCell(c)}(X{p});  // Only valid on parallelograms

InitU: ∀c∈cells(),
	if (norm(Xc{c} - vectOne) < 0.5)
		u^{n}{c} = u0;
	else 
		u^{n}{c} = 0.0; // Initial circle in the center with value u0

InitD: ∀c∈cells(), D{c} = 1.0;

ComputeDeltaTn: δt = Min{c∈cells()}(V{c}/D{c}) * 0.24;
ComputeV: ∀c∈cells(), V{c} = 0.5 * ∑{p∈nodesOfCell(c)}(det(X{p}, X{p+1}));
ComputeFaceLength: ∀f∈faces(), faceLength{f} = 0.5 * ∑{p∈nodesOfFace(f)}(norm(X{p} - X{p+1}));
ComputeFaceConductivity: ∀f∈faces(), faceConductivity{f} = 2.0 * ∏{c1∈cellsOfFace(f)}(D{c1}) / ∑{c2∈cellsOfFace(f)}(D{c2});

ComputeAlphaExtraDiag: ∀f∈innerFaces(), ∀c∈backCell(f), ∀d∈frontCell(f),
	αExtraDiag{f} = δt / V{c} * (faceLength{f} *  faceConductivity{f}) / norm(Xc{c} - Xc{d});

// Assembling of the diffusion matrix
AssembleAlphaDiag: ∀c∈cells(), α{c, c} = 1 - ∑{f∈facesOfCell(c)}(αExtraDiag{f});
AssembleAlphaExtraDiag: ∀f∈innerFaces(), ∀c∈backCell(f), ∀d∈frontCell(f), {
		α{c, d} = αExtraDiag{f};
		α{d, c} = αExtraDiag{f};
}

UpdateU: ∀c∈cells(), u^{n+1}{c} = α{c, c} * u^{n}{c} + ∑{d∈neighbourCells(c)} (α{c, d} * u^{n}{d});
ComputeTn: t^{n+1} = t^{n} + δt;
