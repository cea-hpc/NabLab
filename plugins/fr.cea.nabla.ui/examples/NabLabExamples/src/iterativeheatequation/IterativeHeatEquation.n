/*******************************************************************************
 * Copyright (c) 2022 CEA
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 * Contributors: see AUTHORS file
 *******************************************************************************/
module IterativeHeatEquation;

with Math.*;
with CartesianMesh2D.*;

def check: bool → bool, (a) → if (a) return true; else exit "Assertion failed";

// Simulation options
real stopTime;
int maxIterations;

let real u0 = 1.0;
let real[2] vectOne = real[2](1.0);
let int maxIterationsK = 1000;
let real ε =  1.0e-8;
let real δt = 0.001;
real t;
real[2] X{nodes}, Xc{cells}; // Position of nodes and cells center of gravity 
real u{cells}; // Temperature
real V{cells}; // Volume of cells
real D{cells}; // Cell centered conductivity
real faceLength{faces}, faceConductivity{faces};
real α{cells, cells};
real residual;

iterate	n while (t^{n+1} < stopTime && n+1 < maxIterations),
		k while (residual > ε && check(k+1 < maxIterationsK));

InitTime: t^{n=0} = 0.0;

InitXc: ∀c∈cells(), Xc{c} = 0.25 * ∑{p∈nodesOfCell(c)}(X{p});  // Only valid on parallelograms

InitU: ∀c∈cells(),
	if (norm(Xc{c} - vectOne) < 0.5)
		u^{n}{c} = u0;
	else 
		u^{n}{c} = 0.0; // Initial circle in the center with value u0

InitD: ∀c∈cells(), D{c} = 1.0;

ComputeDeltaTn: δt = Min{c∈cells()}(V{c}/D{c}) * 0.1;
ComputeV: ∀j∈cells(), V{j} = 0.5 * ∑{p∈nodesOfCell(j)}(det(X{p}, X{p+1}));
ComputeFaceLength: ∀f∈faces(), faceLength{f} = 0.5 * ∑{p∈nodesOfFace(f)}(norm(X{p} - X{p+1}));
ComputeFaceConductivity: ∀f∈faces(), faceConductivity{f} = 2.0 * ∏{c1∈cellsOfFace(f)}(D{c1}) / ∑{c2∈cellsOfFace(f)}(D{c2});

// Assembling of the diffusion matrix
ComputeAlphaCoeff: ∀c∈cells(), {
	let real αDiag = 0.0;
	∀d∈neighbourCells(c), ∀f∈commonFace(c,d), {
		let real αExtraDiag = δt / V{c} * (faceLength{f} *  faceConductivity{f}) / norm(Xc{c} - Xc{d});
		α{c, d} = αExtraDiag;
		αDiag = αDiag + αExtraDiag;
	}
	α{c, c} = -αDiag;
}

UpdateU: ∀c∈cells(), u^{n+1, k+1}{c} = u^{n}{c} + α{c, c} * u^{n+1, k}{c} + ∑{d∈neighbourCells(c)} (α{c, d} * u^{n+1, k}{d});
ComputeResidual: residual = Max{j ∈ cells()}(abs(u^{n+1,k+1}{j} - u^{n+1,k}{j}));
ComputeTn: t^{n+1} = t^{n} + δt;
