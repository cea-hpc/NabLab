/*******************************************************************************
 * Copyright (c) 2020 CEA
 * This program and the accompanying materials are made available under the 
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 * Contributors: see AUTHORS file
 *******************************************************************************/
module fr.cea.nabla.GenerateNabla

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = "../.."
var pluginsPath = "${rootPath}/plugins"
var testsPath = "${rootPath}/tests"

Workflow {
	 
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "fr.cea.nabla"
				rootPath = pluginsPath 
				runtimeTest = {
					enabled = true
					root = "${testsPath}/fr.cea.nabla.tests"
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = false
				}
				createEclipseMetaData = true
			}
			code = {
				// encoding = "windows-1252"
				encoding ="UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}

		language = StandardLanguage {
			name = "fr.cea.nabla.Nabla"
			fileExtensions = "n"

			serializer = {
				generateStub = false
			}

			emfGenerator = ecore.EMFGeneratorFragment2 auto-inject {
				generateEdit = true
				editPluginID = "fr.cea.nabla.edit"
				editDirectory = "fr.cea.nabla.edit/src-gen"
				generateEditor = true
				editorPluginID = "fr.cea.nabla.editor"
				editorDirectory = "fr.cea.nabla.editor/src-gen"
			}

//			validator = {
//				composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
//			}

			scopeProvider = scoping.ImportNamespacesScopingFragment2 auto-inject { }
			fragment = exporting.QualifiedNamesFragment2 auto-inject { }
			fragment = ui.templates.CodetemplatesGeneratorFragment2 auto-inject { }
		}

		language = StandardLanguage {
			name = "fr.cea.nabla.Nablagen"
			fileExtensions = "ngen"
			referencedResource = "platform:/resource/fr.cea.nabla/src/fr/cea/nabla/Nabla.xtext"

			serializer = {
				generateStub = false
			}

			emfGenerator = ecore.EMFGeneratorFragment2 {
				generateEdit = true
				editPluginID = "fr.cea.nabla.edit"
				editDirectory = "fr.cea.nabla.edit/src-gen"
				generateEditor = true
				editorPluginID = "fr.cea.nabla.editor"
				editorDirectory = "fr.cea.nabla.editor/src-gen"
			}

			validator = {
				composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}

			scopeProvider = scoping.ImportNamespacesScopingFragment2 auto-inject { }
			fragment = exporting.QualifiedNamesFragment2 auto-inject { }
		}
	}

	// To be able to generate edit an editor source code
	bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
		platformUri = ".."
	}
}
