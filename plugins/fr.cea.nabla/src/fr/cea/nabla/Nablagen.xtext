/*******************************************************************************
 * Copyright (c) 2021 CEA
 * This program and the accompanying materials are made available under the 
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 * Contributors: see AUTHORS file
 *******************************************************************************/

grammar fr.cea.nabla.Nablagen
with fr.cea.nabla.Nabla

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.cea.fr/nabla/Nabla" as nabla

generate nablagen "http://www.cea.fr/nabla/Nablagen"

NablagenRoot: NablagenProviderList | NablagenApplication;

NablagenProviderList: elements += NablagenProvider+;

NablagenProvider:
	'Provider' name=ID ':' extension=[nabla::NablaExtension]
	'{'
		'target' '=' target=TargetType ';'
		('compatibleTargets' '=' compatibleTargets+=TargetType (',' compatibleTargets+=TargetType)* ';')?
		('outputPath' '=' outputPath=STRING ';')? // no output path if native (mesh)
	'}';

NablagenApplication:
	'Application' name=ID ';'
	mainModule=MainModule
	additionalModules+=AdditionalModule*
	extensionConfigs+=ExtensionConfig*
	(vtkOutput=VtkOutput)?
	(levelDB=LevelDB)?
	targets+=Target*;

NablagenModule: MainModule | AdditionalModule;

MainModule:
	'MainModule' type=[nabla::NablaModule] name=ID
	'{'
		'nodeCoord' '=' nodeCoord=[nabla::ConnectivityVar]';'
		'time' '=' time=[nabla::SimpleVar]';'
		'timeStep' '=' timeStep=[nabla::SimpleVar]';'
		// The two following variables are just needed for the C++ display (progression bar)
		('iterationMax' '=' iterationMax=[nabla::SimpleVar] ';'
		'timeMax' '=' timeMax=[nabla::SimpleVar] ';')?
	'}';

AdditionalModule:
	'AdditionalModule' type=[nabla::NablaModule] name=ID
	'{'
		varLinks+=VarLink*
	'}';

VarLink:
	additionalModule=[AdditionalModule] '.' additionalVariable=[nabla::Var] '=' mainModule=[MainModule] '.' mainVariable=[nabla::Var] ';';

VtkOutput:
	'VtkOutput'
	'{'
		'periodReferenceVariable' '=' periodReferenceModule=[MainModule] '.'periodReferenceVar=[nabla::ArgOrVar] ';'
		'outputVariables' '=' vars+=OutputVar (',' vars+=OutputVar)* ';'
	'}';

OutputVar:
	moduleRef=[NablagenModule] '.' varRef=[nabla::ConnectivityVar] 'as' varName=STRING;

/* 
 * variables can be empty for Java and interpreter
 * but it triggers the generation of serialize instructions.
 */
LevelDB:
	{LevelDB} 'LevelDB'
	'{'
		variables+=TargetVar*
	'}';

/*
 * Java is the first enum literal i.e. the default value for interpreter.
 * Thanks to this, interpreter is considered as a Target.
 * 
 * Validation checks that there is a unique interpreter.
 * If multiple interpreter configurations are needed, the launcher
 * should integrate the outputPath as a new launch parameter
 * to identify which interpreter to launch.
 */
Target:
	(writeIR?='[WriteIr]')?
	(interpreter?='Interpreter' | type=TargetType)
	'{'
		'outputPath' '=' outputPath=STRING ';'
		extensionConfigs+=ExtensionConfig*
		variables+=TargetVar*
	'}';

ExtensionConfig:
	'extension' extension=[nabla::NablaExtension] 'providedBy' provider=[NablagenProvider] ';';

TargetVar:
	key=ID '=' value=STRING ';';

// Java must be the first enum literal to be the default value for interpreter
enum TargetType: Java | CppSequential | StlThread | OpenMP | Kokkos | KokkosTeamThread | Arcane | Python;
	