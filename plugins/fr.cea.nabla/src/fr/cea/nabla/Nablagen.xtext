/*******************************************************************************
 * Copyright (c) 2020 CEA
 * This program and the accompanying materials are made available under the 
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 * Contributors: see AUTHORS file
 *******************************************************************************/

grammar fr.cea.nabla.Nablagen 
with fr.cea.nabla.Nabla 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.cea.fr/nabla/Nabla" as nabla

generate nablagen "http://www.cea.fr/nabla/Nablagen"

NablagenModule:
	config=NablagenConfig;

NablagenConfig:
	(writeIR?='[WriteIr]')?
	'nablagen' 'for' nablaModule=[nabla::NablaModule] ';'
	simulation = Simulation
	(vtkOutput = VtkOutput)?
	(levelDB = LevelDB)?
	targets += Target*;

Simulation:
	'SimulationVariables'
	'{'
		'meshClassName' '=' meshClassName=STRING ';'
		'nodeCoord' '=' nodeCoord=[nabla::ConnectivityVar]';'
		'time' '=' time=[nabla::SimpleVar]';'
		'timeStep' '=' timeStep=[nabla::SimpleVar]';'
		// The two following variables are just needed for the C++ display (progression bar)
		('iterationMax' '=' iterationMax=[nabla::SimpleVar] ';'
		'timeMax' '=' timeMax=[nabla::SimpleVar] ';')?
	'}';

VtkOutput:
	'VtkOutput'
	'{'
		'periodReferenceVariable' '=' periodReference=[nabla::ArgOrVar] ';'
		'outputVariables' '=' vars += OutputVar (',' vars += OutputVar)* ';'
	'}';
	
LevelDB:
	'LevelDB'
	'{'
		'levelDBPath' '=' levelDBPath = STRING ';'
	'}';

OutputVar:
	varRef=[nabla::ConnectivityVar] 'as' varName=STRING;

Target: Java | Cpp;
Cpp: CppSequential | CppStlThread | CppOpenMP | CppKokkos | CppKokkosTeamThread;

Java: 'Java'
'{'
	'outputPath' '=' outputDir=STRING ';'
'}';

CppSequential: 'CppSequential'
'{'
	'outputPath' '=' outputDir=STRING ';'
	'compiler' '=' compiler=CppCompiler ';'
	('compilerPath' '=' compilerPath = STRING ';')? 
'}';

CppStlThread: 'StlThread'
'{'
	'outputPath' '=' outputDir=STRING ';'
	'compiler' '=' compiler=CppCompiler ';'
	('compilerPath' '=' compilerPath = STRING ';')? 
'}';

CppOpenMP: 'OpenMP'
'{'
	'outputPath' '=' outputDir=STRING ';'
	'compiler' '=' compiler=CppCompiler ';'
	('compilerPath' '=' compilerPath = STRING ';')? 
'}';

CppKokkos: 'Kokkos'
'{'
	'outputPath' '=' outputDir=STRING ';'
	'kokkosPath' '=' kokkosPath = STRING ';' 
	'compiler' '=' compiler=CppCompiler ';'
	('compilerPath' '=' compilerPath = STRING ';')? 
'}';

CppKokkosTeamThread: 'KokkosTeamThread'
'{'
	'outputPath' '=' outputDir=STRING ';'
	'kokkosPath' '=' kokkosPath = STRING ';' 
	'compiler' '=' compiler=CppCompiler ';'
	('compilerPath' '=' compilerPath = STRING ';')? 
'}';

enum CppCompiler: GNU | LLVM;
