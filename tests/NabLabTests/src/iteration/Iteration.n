/*******************************************************************************
 * Copyright (c) 2021 CEA
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 * Contributors: see AUTHORS file
 *******************************************************************************/
module Iteration;

with CartesianMesh2D.*;
with Assert.*;

let ℝ maxTime = 0.1;
let ℝ δt = 1.0;

ℝ t;
ℝ[2] X{nodes};

let ℕ maxIterN = 10;
let ℕ maxIterK = 6;
let ℕ maxIterL = 7;

ℝ vn{cells}, vk{cells}, vl{cells};

iterate n while (n < maxIterN), { 
			k while (k < maxIterK);
			l while (l < maxIterL);
		}

IniTime: t^{n=0} = 0.0;

IniVn: ∀j∈cells(), vn^{n=0}{j} = 0.0;
UpdateVn: ∀j∈cells(), vn^{n+1}{j} = vn^{n}{j} + vl^{n+1}{j} * 2;

IniVk: ∀j∈cells(), vk^{n+1, k=0}{j} = 0.0;
UpdateVk: ∀j∈cells(), vk^{n+1, k+1}{j} = vk^{n+1, k}{j} + 2;
OracleVk: ∀j∈cells(), let ℾ testVk = assertEquals(12.0, vk^{n+1}{j});

IniVl: ∀j∈cells(), vl^{n+1, l=0}{j} = vk^{n+1}{j} + 8;
UpdateVl: ∀j∈cells(), vl^{n+1, l+1}{j} = vl^{n+1, l}{j} + 1;
OracleVl: ∀j∈cells(), let ℾ testVl = assertEquals(27.0, vl^{n+1}{j});

OracleVn: ∀j∈cells(), let ℾ testVn = assertEquals(54.0*n, vn^{n+1}{j});

ComputeTn: t^{n+1} = t^{n} + δt;
